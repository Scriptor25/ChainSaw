\inc "complex.csh"
\inc "image.csh"
\inc "std/math.csh"
\inc "std/stdbool.csh"
\inc "std/stdio.csh"
\inc "std/stdlib.csh"

@[c]strcmp(str1: int8*, str2: int8*): int32;
@"=="(str1: int8*, str2: int8*): bool = strcmp(str1, str2) == 0;
@"!="(str1: int8*, str2: int8*): bool = strcmp(str1, str2) != 0;

@random::flt64 = (rand() % RAND_MAX) / (RAND_MAX - 1.0);

@mandel(z0: complex, max: int32): int32 {
    z: complex = z0;
	t: int32;
    for (t = 0; (t < max) && (z.abs() <= 2); ++t)
        z = z.square() + z0;
    ret t;
}

MAX_ITER: int32 = 100;
SAMPLES:  int32 = 100;

@[c]main::int32 {

    xc:     flt64;
    yc:     flt64;
    size:   int32;
    width:  int32;
    height: int32;

    printf("xc: ");
    scanf("%lf", &xc);  ## -0.5
    printf("yc: ");
    scanf("%lf", &yc);  ##    0
    printf("size: ");
    scanf("%d", &size); ##    2

    printf("width: ");
    scanf("%d", &width);
    printf("height: ");
    scanf("%d", &height);

    printf("Rendering with xc=%f yc=%f size=%d width=%d height=%d\n", xc, yc, size, width, height);
    printf("Continue? ([Y]|N) ");

    confirm: int8;
    scanf("%c", &confirm);
    if (confirm == 'n' || confirm == 'N')
        ret 0;

    out: image = image(width, height);

    for (j: int32; j < out.height; ++j) {
        printf("\rScanlines remaining: %5d", out.height - j);
        for (i: int32; i < out.width; ++i) {
            n: int32 = 0;
            for (s: int32; s < SAMPLES; ++s) {
				x0: flt64 = xc - size / 2 + size * (i + random()) /  out.width;
                y0: flt64 = yc - size / 2 + size * (j + random()) / out.height;
                z0: complex = complex(x0, y0);
                n += mandel(z0, MAX_ITER);
            }
            grey: int32 = [int32](255.999 * n / (SAMPLES * MAX_ITER));
            out.pixel(i, j, (grey & 0xff) << 16 | (grey & 0xff) << 8 | (grey & 0xff));
        }
    }

    out.toppm("mandel.ppm");
    out.destroy();

    printf("\rDone.                       \n");
    ret 0;
}
